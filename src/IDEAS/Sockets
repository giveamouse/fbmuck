MUF Prims for TCP Sockets
	TCP_LISTEN[  int:port             -- str:listenid ]
	TCP_ACCEPT[  str:listenid         -- str:sockid   ]
	TCP_CONNECT[ str:host int:port    -- str:sockid   ]
	TCP_SEND[    str:data str:sockid  --              ]
	TCP_CLOSE[   str:sockid           --              ]
	TCP_STATUS[  str:sockid           -- str:status   ]

	TCP_SETMODE[ str:sockid str:name int:value --     ]
	TCP_GETMODE[ str:sockid str:name  -- varies:value ]


: TCP_READ[ str:sockid int:timeout -- str:data int:success ]
	var timer

	timeout @ 0 >= if
		"__tcpread_" sockid @ strcat timer !
	    timeout @ timer @ TIMER_START
	then
    {
		"TCP.READ." sockid @ strcat
		"TCP.ERROR." sockid @ strcat
		"TCP.CLOSED." sockid @ strcat
		"TIMER." timer @ strcat
	}list
	ARRAY_WAITFOR
	dup "TCP.ERROR." 10 strncmp if
		dup "TCP.CLOSED." 11 strncmp if
			"TIMER." 6 strncmp if
				"data" [] 1
			else
				pop "TCP_READ: Timed out." abort
			then
		else
			pop pop "TCP_READ: Socket closed by remote host." abort
		then
	else
	    pop "error" [] "TCP_READ: " swap strcat abort
	then
;


MUF Events for TCP
	"TCP.CONNECT.sockid"  Queued when a new connection request is received.
	"TCP.READ.sockid"     Queued when a new line is received.
	"TCP.CLOSED.sockid"   Queued when the remote side closed the socket.
	"TCP.ERROR.sockid"    Queued when there is an error that closes the socket.



==============================================================================

( Defines to make ProtoMUCK sockets code run with the proposed FB6 sockets. )
( This should probably be in a $lib/protomuck/sockets library or something. )

$ifdef __fuzzball__
	$define sockopen
		10 "__tcpto_" TIMER_START
		TCP_CONNECT
		{
			"TCP.CONNECT." 3 pick strcat
			"TIMER.__tcpto_"
		}list event_waitfor
		"TIMER.__tcpto_" strcmp not if
			pop "Connection timed out."
		else
			"__tcpto_" TIMER_STOP
			pop "noerr"
		then
	$enddef

	$define nbsockopen
		2 try 
			TCP_CONNECT
			"Operation now in progress"
		catch
			"" swap
		endcatch
	$enddef

	$define sockrecv
		"TCP.READ." swap strcat
		dup EVENT_EXISTS if
			1 ARRAY_MAKE EVENT_WAITFOR
			pop "data" []
		else
			pop ""
		then
	$enddef

	$def sockcheck TCP_STATUS "Open" stringcmp not
	$def socksend 2 try TCP_SEND 1 catch pop 0 endcatch
	$def sockclose 1 try TCP_CLOSE 1 catch pop 0 endcatch
	$def socket? 1 try TCP_STATUS pop 1 catch pop 0 endcatch
$endif



==============================================================================

( Sample POP3 client written in MUF )

$def LOGIN_STATE   1
$def AUTH_STATE    2
$def ACTIVE_STATE  3

lvar conns
lvar listenport

: handle-connect[ dict:args str:eventtype -- ]
	args @ "id" [] TCP_ACCEPT var! sockid

	conns @ sockid @
	{
		"state" LOGIN_STATE
		"data"  ""
		"user"  ""
	}dict
	array_setitem conns !
;
  
: getconvalue[ str:id str:key -- any:val ]
	conns @ id @ [] key @ []
;

: putconvalue[ str:id str:key any:val -- ]
	conns @ id @ []
	key @ val @ array_setitem
	conns @ id @ rot array_setitem
	conns !
;

: close_conn[ str:id -- ]
	id @ TCP_CLOSE
	conns @ id @ array_delitem
	conns !
;

: process_line[ str:id str:line -- ]
	line @ " " split var! args
	tolower var! cmd
	cmd @ "quit" strcmp not if
		( Delete marked mail, and disconnect. )
		"+OK\r\n" id @ TCP_WRITE
		id @ close_conn
		exit
	then
	cmd @ "user" strcmp not if
		id @ "state" getconvalue
		LOGIN_STATE = if
			id @ "user" args @ putconvalue
			id @ "state" AUTH_STATE putconvalue
			"+OK\r\n" id @ TCP_WRITE
		    exit
		then
		"-ERR User was already specified.\r\n" id @ TCP_WRITE
		exit
	then
	cmd @ "pass" strcmp not if
		id @ "state" getconvalue
		AUTH_STATE = if
			id @ "user" getconvalue pmatch var! user
			user @ if
				user @ args @ checkpassword if
					id @ "state" ACTIVE_STATE putconvalue
					"+OK\r\n" id @ TCP_WRITE
					exit
				then
			then
		then
		"-ERR Bad username or Password\r\n" id @ TCP_WRITE
		id @ close_conn
		exit
	then
	id @ "state" getconvalue
	ACTIVE_STATE = if
		cmd @ "uidl" strcmp not if
			( List mail messages waiting, with message ids )
			"+OK\r\n" id @ TCP_WRITE
			exit
		then
		cmd @ "list" strcmp not if
			( List mail messages waiting )
			"+OK\r\n" id @ TCP_WRITE
			exit
		then
		cmd @ "stat" strcmp not if
			( I dunno what this is supposed to do. )
			"+OK\r\n" id @ TCP_WRITE
			exit
		then
		cmd @ "rset" strcmp not if
			( I dunno what this is supposed to do. )
			"+OK\r\n" id @ TCP_WRITE
			exit
		then
		cmd @ "retr" strcmp not if
			args @ atoi var! mesgnum
			( Spew out a mail message )
			"+OK\r\n" id @ TCP_WRITE
			exit
		then
		cmd @ "top" strcmp not if
			args @ " " split
			atoi var! lines
			atoi var! mesgnum
			( Spew out a length limited mail message )
			"+OK\r\n" id @ TCP_WRITE
			exit
		then
		cmd @ "dele" strcmp not if
			args @ atoi var! mesgnum
			( Mark the mail message for deletion )
			"+OK\r\n" id @ TCP_WRITE
			exit
		then
		exit
	then
	"-ERR Not authenticated.\r\n" id @ TCP_WRITE
	id @ close_conn
;

: handle-close[ dict:args str:eventtype -- ]
	args @ "id"   [] var! sockid
	sockid @ close_conn
;

: handle-read[ dict:args str:eventtype -- ]
	args @ "id"   [] var! sockid
	args @ "data" [] var! data

	conns @ sockid @ [] var! conn

	conn @ "data" [] data @ strcat data !
	begin
		data @
		dup "\r\n" instr while
		"\r\n" split data !
		sockid @ swap process_line
	repeat
	conn @ "data" data @ array_setitem conn !
;

: pop-server[ str:arg -- ]
    { }dict conns !

	8110 TCP_LISTEN listensock !
	{
	}dict
	{
		"TCP.CONNECT." listensock @ strcat 'handle-connection
		"TCP.ERROR.*"   'handle-close
		"TCP.READ.*"    'handle-read
		"TCP.CLOSED.*"  'handle-close
	}dict
	process_events
;

