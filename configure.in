dnl
dnl Process this file with autoconf to produce a configure script.
dnl
dnl TinyMUCK fb5.x auto-config script writen by: Peter "WhiteFire" Torkelson
dnl Revised for fb6.x by revar@belfry.com
dnl
AC_REVISION($Revision$)dnl
AC_INIT(src/game.c)
AC_CONFIG_HEADER(include/autoconf.h)
echo " "
echo "TinyMUCK fb6.x auto-configure script."
echo " "
echo "This script will try and determine things about your system so"
echo "that FB can compile correctly. This will create your Makefile"
echo "and the header file autoconf.h in the include directory."
echo " "

dnl
dnl Find the compiler first.
dnl
AC_PROG_CC
AC_PROG_CPP
dnl AC_PROG_CXX for finding the C++ compiler.

dnl
dnl Specific systems tests.
dnl
AC_ISC_POSIX
AC_MINIX

AC_ARG_WITH(ssl,
[  --with-ssl[=ssltop_path]   use ssl encryption and authorization],
[AC_DEFINE(USE_SSL)
if test "$withval" = yes; then
  SSLTOP=/usr/local/ssl
else
  SSLTOP=$withval
fi
LIBS="-L$SSLTOP/lib -lssl -lcrypto $LIBS"
INC="-I$SSLTOP/include -I$SSLTOP/include/openssl $INC"
AC_DEFINE(USE_SSL)
AC_SUBST(DEFS)
AC_SUBST(INC)
AC_SUBST(SSLTOP)
AC_CHECK_HEADERS(ssl.h ssl/ssl.h openssl/ssl.h)
])

AC_ARG_WITH(lint,
[  --with-lint[=lint_path]    use lint at lint_path, defaults to splint],
[AC_DEFINE(LINT)
if test "$withval" = yes; then
  LINT=splint
else
  LINT=$withval
fi
AC_DEFINE(WITH_LINT)
AC_SUBST(LINT)
])

dnl
dnl Header files
dnl
AC_CHECK_HEADERS(malloc.h memory.h string.h unistd.h sys/resource.h sys/signal.h)
AC_CHECK_HEADERS(sys/time.h timebits.h)
AC_CHECK_HEADERS(varargs.h stdarg.h)
AC_HEADER_STDC
AC_HEADER_DIRENT
AC_HEADER_TIME
AC_CHECK_HEADERS(errno.h sys/errno.h)
echo "checking for tm_gmtoff"
AC_EGREP_HEADER([[^_a-zA-Z]]tm_gmtoff;, time.h, AC_DEFINE(HAVE_TM_GMTOFF))
AC_EGREP_HEADER([[^_a-zA-Z]]tm_gmtoff;, sys/time.h, AC_DEFINE(HAVE_SYS_TM_GMTOFF))
AC_EGREP_HEADER([[^_a-zA-Z]]_timezone\>, time.h, AC_DEFINE(HAVE__TIMEZONE))

dnl
dnl Libraries and functions.
dnl
AC_CHECK_LIB(m, cos)
AC_CHECK_LIB(socket, shutdown)
AC_CHECK_LIB(nsl, gethostbyname)
AC_FUNC_VPRINTF
AC_CHECK_FUNCS(mallinfo getrlimit getrusage random snprintf vsnprintf)
AC_EGREP_HEADER([[^_a-zA-Z]]hblks;, malloc.h, AC_DEFINE(HAVE_MALLINFO_HBLKS))
AC_EGREP_HEADER([[^_a-zA-Z]]keepcost;, malloc.h, AC_DEFINE(HAVE_MALLINFO_KEEPCOST))
AC_EGREP_HEADER([[^_a-zA-Z]]treeoverhead;, malloc.h, AC_DEFINE(HAVE_MALLINFO_TREEOVERHEAD))
AC_EGREP_HEADER([[^_a-zA-Z]]grain;, malloc.h, AC_DEFINE(HAVE_MALLINFO_GRAIN))
AC_EGREP_HEADER([[^_a-zA-Z]]allocated;, malloc.h, AC_DEFINE(HAVE_MALLINFO_ALLOCATED))

dnl
dnl Types and structures
dnl
AC_TYPE_PID_T
AC_TYPE_SIZE_T
AC_TYPE_SIGNAL
AC_STRUCT_TIMEZONE

dnl
dnl Compiler characteristics
dnl
AC_C_CONST
AC_C_INLINE
AC_C_LONG_DOUBLE

dnl
dnl Uname -a, just because.
dnl
echo "checking value of uname -a"
AC_DEFINE_UNQUOTED(UNAME_VALUE, "`uname -a`")

dnl
dnl And in the end, there was no more.
dnl
AC_OUTPUT(
	Makefile
	src/Makefile
	game/restart
)
echo " "
echo "You should review the options in include/config.h, and"
echo "then type make to build your system."
echo " "
