( cmd-@teleport by Natasha@HLM
  A simple @teleport command for Fuzzball 6, thanks to the magic of exceptions.
 
  You can teleport anything anywhere as long as it's within the rules of the
  "moveto" primitive. Type "man moveto" to see the three rules. Unfortunately
  this means wizards can't @teleport things willy-nilly; to do that, they
  must type "!@tel" to use the in-server @teleport command. On HLM we decided
  that's actually a good thing, so there you go.
 
  Since the program's protections are based on the limits of the moveto prim,
  the program should NOT be set above M2, including W. Sorry, you can't
  customize the permission-related error messages: they're generated by
  Fuzzball itself.
 
  Copyright 2002 Natasha O'Brien. Copyright 2002 Here Lie Monsters.
  "@view $box/mit" for licensing information.
)
$author Natasha O'Brien <mufden@mufden.fuzzball.org>
$version 1.001
$note A simple @teleport command for Fuzzball 6, using exceptions.
 
$include $lib/strings
: rtn-match  ( str -- db )
    dup "#" stringpfx if  ( str )
        stod  ( db )
    else  ( str )
        match  ( db )
    then
;
 
: main  ( str -- )
    dup not if pop .showhelp exit then  ( str )
    dup strip "#help" stringcmp not if pop .showhelp exit then  ( str )
 
    "=" "" tokensplit pop  ( strY strZ )
 
    ( If '@tel foo', assume 'me=foo' instead. )
    dup not if pop "me" swap then  ( strY strZ )
 
    1 2 1 for pop  ( strY strZ )
        dup rtn-match dup ok? not if  ( strY strZ dbZ )
            pop "I don't know what '%s' is." fmtstring .tell  ( strY )
            pop exit  (  )
        then swap pop  ( strY dbZ )
        ( At this point we can do it again, only with dbZ for strY and strY for strZ. )
        swap
    repeat  ( dbY dbZ )
 
    ( Do extra checks here. )
 
    0 try
        over over moveto  ( dbY dbZ )
    catch  ( dbY dbZ strErr )
        swap rot  ( strErr dbZ dbY )
        "Could not teleport %D to %D: %s" fmtstring .tell  (  )
        exit  (  )
    endcatch  ( dbY dbZ )
    swap "Teleported %D to %D." fmtstring .tell
;
